/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment
    Inc16(in=reg, out=reginc);
    Mux16(a=reg, b=reginc, sel=inc, out=reg1);

    // load value
    Mux16(a=reg1, b=in, sel=load, out=regload);

    // reset
    Mux16(a=regload, b=false, sel=reset, out=regreset);

    // save value
    Register(in=regreset, out=out, out=reg, load=true);
}
